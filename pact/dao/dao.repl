(begin-tx)
(env-data
 { 'ns-admin-keyset: ["admin"]
 , 'ns-operate-keyset: ["operate"]
 , 'ns-genesis-keyset: { "keys": [], "pred": "="} })
(load "../root/fungible-v2.pact")
(load "../root/coin.pact")
(load "../root/ns.pact")

(env-data
  { 'dao-ns-user: ["dao-user"]
  , 'dao-ns-admin: ["dao-admin"]
  , 'dao-ns: "dao"
  , 'upgrade: false
  })
(env-keys ["operate", "dao-user", "dao-admin"])

(load "dao-ns.pact")

(env-data { 'dao-ns: "dao" })
(env-chain-data
  {"block-height": 1675409
  ,"block-time": (time "2021-06-01T12:00:00Z")
  ,"chain-id": "0"
  ,"gas-limit": 10000000
  ,"gas-price": 0.0
  ,"prev-block-hash": ""
  ,"sender": ""})
(load "dao.pact")
(typecheck "dao.dao-v1" true)

; Check init State
(coin.details DAO_ACCT_NAME)
(use dao.dao-v1)
(view-state)
(view-guardians)
(view-ambassadors)
(is-dao-frozen)
(expect "state hash checkpoint" "qp44Pd5Mzox-D2YiGakiC-zTzLeyjrFuyXXAnyR3duU" (state-hash))
(commit-tx)

(begin-tx)
(load "tests/populate-users.repl")
(use dao.dao-v1)

; -----------------------------------------------------------------------------
; basic guardian functionality tests
; -----------------------------------------------------------------------------
(env-sigs [
  { 'key: 'guardian0
  , 'caps:
      [(coin.TRANSFER 'guardian0 DAO_ACCT_NAME GUARDIAN_KDA_REQUIRED)]}
  { 'key: 'guardian1
  , 'caps:
      [(coin.TRANSFER 'guardian1 DAO_ACCT_NAME GUARDIAN_KDA_REQUIRED)]}
  { 'key: 'guardian2
  , 'caps:
      [(coin.TRANSFER 'guardian2 DAO_ACCT_NAME GUARDIAN_KDA_REQUIRED)]}
  ])
(register-guardian 'guardian0 (read-keyset 'guardian0))
(register-guardian 'guardian1 (read-keyset 'guardian1))
(register-guardian 'guardian2 (read-keyset 'guardian2))

(expect "3 guardians" 3 (at 'guardian-count (view-state)))
(expect "DAO has right balance" (* 3 GUARDIAN_KDA_REQUIRED) (DAO_ACCT_BALANCE))
(expect-failure "cannot unregister guardians" (unregister-guardian 'guardian1))

(env-sigs [
  { 'key: 'bob
  , 'caps:
      [(coin.TRANSFER 'bob DAO_ACCT_NAME GUARDIAN_KDA_REQUIRED)]
  }])
(expect-failure "insufficient funds for guardianship" (register-guardian 'bob (read-keyset 'bob)))
(view-guardians)
(expect "state hash checkpoint" "GUM-RCyDuFdfdrKIR2U4RR6Sa9MenRM3N_Va39OBBT0" (state-hash))

; -----------------------------------------------------------------------------
; basic ambassador functionality tests
; -----------------------------------------------------------------------------

(env-sigs [
  { 'key: 'guardian0
  , 'caps:[(GUARDIAN 'guardian0)]}
  ])
(register-ambassador 'guardian0 'ambassador0 (read-keyset 'ambassador0))
(register-ambassador 'guardian0 'ambassador1 (read-keyset 'ambassador1))
(register-ambassador 'guardian0 'ambassador2 (read-keyset 'ambassador2))
(register-ambassador 'guardian0 'ambassador3 (read-keyset 'ambassador3))
(expect "4 ambassadors" 4 (at 'ambassador-count (view-state)))

(deactivate-ambassador 'guardian0 'ambassador3)
(expect "3 ambassadors" 3 (at 'ambassador-count (view-state)))

(expect-failure "cooldown triggered" (deactivate-ambassador 'guardian0 'ambassador2))
(env-chain-data
  {"block-time": (add-time
      (at 'last-ambassador-deactivation (view-state))
      (+ DEACTIVATE_COOLDOWN (days 1)))})
(expect "cooldown triggered" true (deactivate-ambassador 'guardian0 'ambassador2))

(reactivate-ambassador 'guardian0 'ambassador2)

; -----------------------------------------------------------------------------
; freezer tests
; -----------------------------------------------------------------------------
(env-sigs [
  { 'key: 'ambassador0
  , 'caps:[(AMBASSADOR 'ambassador0)]},
  { 'key: 'ambassador1
  , 'caps:[(AMBASSADOR 'ambassador1)]},
  { 'key: 'ambassador2
  , 'caps:[(AMBASSADOR 'ambassador2)]}
  ])
(env-chain-data
  {"block-time": (time "2021-07-01T12:00:00Z")})

(vote-to-freeze 'ambassador0)
(expect-failure "insufficent votes" (freeze 'ambassador0))
(expect "not frozen" false (is-dao-frozen))

(vote-to-freeze 'ambassador1)
(freeze 'ambassador0)
(expect-failure "DAO is frozen" (is-dao-frozen))

; roll forward to the edge of the boundry
(env-chain-data
  {"block-time": (time "2021-07-08T11:59:59Z")})
(expect-failure "DAO is frozen" (is-dao-frozen))
(expect-failure "freeze votes are timed out" (freeze 'ambassador0))

; check that ambassadors can't refreeze while frozen
(vote-to-freeze 'ambassador0)
(vote-to-freeze 'ambassador1)
(expect-failure "cannot freeze a frozen DAO" (freeze 'ambassador0))

; check that freeze aborts GOVERNANCE
(expect-failure "GOVERNANCE cap is frozen" (read state DAO_STATE_KEY))

; roll forward the first second that the DAO is unfrozen
(env-chain-data
  {"block-time": (time "2021-07-08T12:00:01Z")})
(expect "DAO is not frozen" false (is-dao-frozen))

; -----------------------------------------------------------------------------
; proposal tests
; -----------------------------------------------------------------------------

(env-sigs [
  { 'key: 'guardian0
  , 'caps:[(GUARDIAN 'guardian0)]}
  { 'key: 'guardian1
  , 'caps:[(GUARDIAN 'guardian1)]}
  ])
(propose-dao-upgrade 'guardian0 "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA")
(expect-failure "approvals still in cooldown"
  (check-hash-approval "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA"))
(env-chain-data
  {"block-time": (time "2021-07-09T12:00:01Z")})
(expect-failure "approvals still in cooldown (but now 1 of 3)"
  (check-hash-approval "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA"))
(expect-failure "wrong hash"
  (guardian-approve-hash 'guardian1 "foo"))
(guardian-approve-hash 'guardian1 "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA")
(check-hash-approval "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA")
(env-chain-data
  {"block-time": (time "2021-07-11T12:00:01Z")})
(expect-failure "Poposal timed out"
  (check-hash-approval "pwOa2NBxVO0wtVt7Xt2oeJOUkBYML_URtZnDHHK6AJA"))
